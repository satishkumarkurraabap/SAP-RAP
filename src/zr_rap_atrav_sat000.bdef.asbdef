managed implementation in class ZBP_R_RAP_ATRAV_SAT000 unique;
strict (2);
with draft;
extensible;
define behavior for ZR_RAP_ATRAV_SAT000 alias ZrRapAtravSat000
persistent table zrap_atrav_sat
with additional save
extensible
draft table zrp_atrv_st000_d
etag master LastChangedAt
lock master total etag LastChangedAt
authorization master ( global )
early numbering
{

  field ( readonly )
  CreatedBy,
  CreatedAt,
  LastChangedBy,
  LastChangedAt;

  field ( readonly )
  TravelID;

  field ( mandatory )
  CustomerID,
  BeginDate,
  EndDate;

  create;
  update ( features : instance );
  delete ( features : instance );


  determination setStatusToOpen on modify { create; }

  validation validateCustomer on save { create; field CustomerID; }
  validation validateDates on save { create; field BeginDate, EndDate; }

  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;

  action ( features : instance ) deductDiscount parameter /dmo/a_travel_discount result [1] $self;
  factory action copyTravel [1];


  draft action Activate optimized;
  draft action Discard;
  draft action ( features : instance ) Edit;
  draft action Resume;


  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateDates; }


  mapping for zrap_atrav_sat corresponding extensible
    {
      TravelID      = travel_id;
      AgencyID      = agency_id;
      CustomerID    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      Attachment    = ATTACHMENT;
      MimeType      = MIME_TYPE;
      FileName      = FILE_NAME;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }

}